Texture2D texture2d : PREVIOUS;
Texture2D prevao;

Texture2D velocity : METALROUGHVEL;

float2 tsize : INVTARGETSIZE;

float Weight<float uimin = 0.0; float uimax = .98;> = .98;
float thresh<string uiname = "Clamp Threshold"; float uistep = .002;>;

SamplerState linearSampler : IMMUTABLE
{
    Filter = MIN_MAG_MIP_LINEAR;
    AddressU = Clamp;
    AddressV = Clamp;
};

SamplerState s_vel : IMMUTABLE
{
	Filter=MIN_MAG_MIP_POINT;
	AddressU=CLAMP;
	AddressV=CLAMP;
	MipLODBias = 0;
};

struct psInput
{
	float4 p : SV_Position;
	float2 uv : TEXCOORD0;
};


float4 PS(psInput input) : SV_Target
{
	float2 uv = input.uv;
	float2 vel = velocity.Sample(s_vel, uv).ba;
	vel -= .5;
	vel.x = lerp(vel.x, 0, step(abs(vel.x), .0005));
	vel.y = -lerp(vel.y, 0, step(abs(vel.y), .0005));
	
	float a0 = texture2d.Sample(linearSampler, input.uv + float2(tsize.x,0)).r;
	float a1 = texture2d.Sample(linearSampler, input.uv + float2(-tsize.x,0)).r;
	float a2 = texture2d.Sample(linearSampler, input.uv + float2(0,tsize.y)).r;
	float a3 = texture2d.Sample(linearSampler, input.uv + float2(0,-tsize.y)).r;
	float a4 = texture2d.Sample(linearSampler, input.uv + float2(tsize.x,tsize.y)).r;
	float a5 = texture2d.Sample(linearSampler, input.uv + float2(-tsize.x,tsize.y)).r;
	float a6 = texture2d.Sample(linearSampler, input.uv + float2(tsize.x,-tsize.y)).r;
	float a7 = texture2d.Sample(linearSampler, input.uv + float2(-tsize.x,-tsize.y)).r;
	
	float aomin = min(min(min(a0, a1), min(a2, a3)), min(min(a4, a5), min(a6, a7)))*(1-thresh);
	float aomax = max(max(max(a0, a1), max(a2, a3)), max(max(a4, a5), max(a6, a7)))*(1+thresh);
	
	float4 dao = texture2d.Sample(linearSampler,uv);
	float ao = dao.r;
	float a = dao.a;
	float pao = prevao.Sample(linearSampler,uv-vel).r;
	pao = clamp(pao, aomin, aomax);
	if(uv.x < 0 || uv.y < 0 || uv.x > 1 || uv.y > 1) pao = ao;
	
	float weight = saturate(Weight * (1 - length(vel) * 16));
	float filtered = lerp(ao, pao, weight);
	return filtered * a;
}

technique10 TempAO<bool persist = true;>
{
	pass P0
	{
		SetPixelShader(CompileShader(ps_4_0,PS()));
	}
}



